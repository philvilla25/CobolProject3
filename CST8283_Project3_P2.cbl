       IDENTIFICATION DIVISION.
           PROGRAM-ID. BUY-STOCKS.
       ENVIRONMENT DIVISION.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT PORTFOLIO-FILE
           ASSIGN TO '../PORTFOLIO.dat'
           ORGANIZATION IS INDEXED
           ACCESS MODE IS RANDOM
           RECORD KEY IS PORTFOLIO-STOCK-SYMBOL.
           SELECT STOCKS-FILE
           ASSIGN TO '../STOCKS.txt'
           ORGANIZATION IS LINE SEQUENTIAL.
       DATA DIVISION.
       FILE SECTION.
       FD PORTFOLIO-FILE.
       01 PORTFOLIO-RECORD.
           02 PORTFOLIO-STOCK-SYMBOL PIC X(7).
           02 NUMBER-OF-SHARES PIC 9(5).
           02 AVERAGE-COST PIC 9(4)V99.
       FD STOCKS-FILE.
       01 STOCKS-RECORD.
           02 SSYMBOL PIC X(7).
           02 SNAME PIC X(25).
           02 CPRICE PIC 9(4)V99.
       WORKING-STORAGE SECTION.
       01 TABLE-INDEX PIC 9(2).
       01 EOF PIC A.
       01 STOCKS-TABLE OCCURS 20 TIMES.
           02 STOCK-SYMBOL PIC X(7).
           02 STOCK-NAME PIC X(25).
           02 CLOSING-PRICE PIC 9(4)V99.
       01 INPUT-STOCK-SYMBOL PIC X(7).
       01 INPUT-NUMBER-OF-SHARES PIC 9(5).
       01 FOUND-FLAG PIC A.
       01 OUTPUT-NUMBER-OF-SHARES PIC 9(5).
       01 OUTPUT-AVERAGE-COST PIC 9(4)V99.
       01 NEW-RECORD PIC A.
       SCREEN SECTION.
       01 INPUT-SCREEN.
           02 LINE 2 COL 10 VALUE 'Buy Stocks'.
           02 LINE 4.
               03 COL 3 VALUE 'Enter a Stock Symbol: '.
               03 COL 25 PIC X(7) TO INPUT-STOCK-SYMBOL.
           02 LINE 5.
               03 COL 3 VALUE 'Enter the Number of Shares: '.
               03 COL 31 PIC X(5) TO INPUT-NUMBER-OF-SHARES.
       01 OUTPUT-SCREEN.
           02 LINE 7 COL 10 VALUE 'Updated Portfolio Record'.
           02 LINE 9.
               03 COL 3 VALUE 'Stock Symbol: '.
               03 COL 17 PIC X(7) FROM PORTFOLIO-STOCK-SYMBOL.
           02 LINE 10.
               03 COL 3 VALUE 'Number of Shares: '.
               03 COL 21 PIC ZZ,ZZ9 FROM OUTPUT-NUMBER-OF-SHARES.
           02 LINE 11.
               03 COL 3 VALUE 'Average Cost: '.
               03 COL 17 PIC $$,$$9.99 FROM OUTPUT-AVERAGE-COST.
           02 LINE 13.
               03 COL 3 VALUE 'Buy more stocks? (Y/N) '.
               03 COL 26 PIC A TO NEW-RECORD.
       PROCEDURE DIVISION.
       MAIN-PROCEDURE.
           PERFORM INITIALIZATION-RTN.
           PERFORM PROCESS-RTN UNTIL NEW-RECORD = 'N'.
           PERFORM CLOSE-FILES.
           STOP RUN.
       INITIALIZATION-RTN.
           PERFORM OPEN-FILES.
           PERFORM LOAD-TABLE VARYING TABLE-INDEX FROM 1 BY 1
               UNTIL EOF = 'Y' OR TABLE-INDEX > 20.
       OPEN-FILES.
           OPEN I-O PORTFOLIO-FILE.
           OPEN INPUT STOCKS-FILE.
       LOAD-TABLE.
           READ STOCKS-FILE
               AT END MOVE 'Y' TO EOF
               NOT AT END
                   MOVE SSYMBOL TO STOCK-SYMBOL(TABLE-INDEX)
                   MOVE SNAME TO STOCK-NAME(TABLE-INDEX)
                   MOVE CPRICE TO CLOSING-PRICE(TABLE-INDEX)
           END-READ.
       PROCESS-RTN.
           PERFORM GET-INPUT.
           PERFORM READ-PORTFOLIO-FILE.
           PERFORM DISPLAY-OUTPUT.
       GET-INPUT.
           ACCEPT INPUT-SCREEN.
           MOVE INPUT-STOCK-SYMBOL TO PORTFOLIO-STOCK-SYMBOL.
       READ-PORTFOLIO-FILE.
           READ PORTFOLIO-FILE
               INVALID KEY PERFORM ADD-RECORD
               NOT INVALID KEY PERFORM UPDATE-RECORD
           END-READ.
       ADD-RECORD.
           PERFORM SET-PORTFOLIO-RECORD.
           PERFORM CALCULATIONS-RTN.
           PERFORM WRITE-RECORD.
       UPDATE-RECORD.
           PERFORM CALCULATIONS-RTN.
           PERFORM REWRITE-RECORD.
       SET-PORTFOLIO-RECORD.
           MOVE 0 TO NUMBER-OF-SHARES.
           MOVE 0 TO AVERAGE-COST.
       CALCULATIONS-RTN.
           PERFORM SET-FOUND-FLAG.
           PERFORM CALCULATIONS VARYING TABLE-INDEX FROM 1 BY 1
               UNTIL FOUND-FLAG = 'Y' OR TABLE-INDEX > 20.
       SET-FOUND-FLAG.
           MOVE 'N' TO FOUND-FLAG.
       CALCULATIONS.
           IF PORTFOLIO-STOCK-SYMBOL = STOCK-SYMBOL(TABLE-INDEX)
               COMPUTE AVERAGE-COST = (NUMBER-OF-SHARES * AVERAGE-COST +
                   INPUT-NUMBER-OF-SHARES * CLOSING-PRICE(TABLE-INDEX))
                   / (NUMBER-OF-SHARES + INPUT-NUMBER-OF-SHARES)
               MOVE AVERAGE-COST TO OUTPUT-AVERAGE-COST
               ADD INPUT-NUMBER-OF-SHARES TO NUMBER-OF-SHARES
               MOVE NUMBER-OF-SHARES TO OUTPUT-NUMBER-OF-SHARES
           END-IF.
       WRITE-RECORD.
           WRITE PORTFOLIO-RECORD.
       REWRITE-RECORD.
           REWRITE PORTFOLIO-RECORD.
       DISPLAY-OUTPUT.
           ACCEPT OUTPUT-SCREEN.
       CLOSE-FILES.
           CLOSE PORTFOLIO-FILE STOCKS-FILE.
       END PROGRAM BUY-STOCKS.
