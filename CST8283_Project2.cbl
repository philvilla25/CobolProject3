       IDENTIFICATION DIVISION.
           PROGRAM-ID. PROJECT-2.
       ENVIRONMENT DIVISION.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT PORTFOLIO-FILE
           ASSIGN TO '../PORTFOLIO.txt'
           ORGANIZATION IS LINE SEQUENTIAL.
           SELECT STOCKS-FILE
           ASSIGN TO '../STOCKS.txt'
           ORGANIZATION IS LINE SEQUENTIAL.
           SELECT REPORT-FILE
           ASSIGN TO '../REPORT.txt'
           ORGANIZATION IS LINE SEQUENTIAL.
       DATA DIVISION.
       FILE SECTION.
       FD PORTFOLIO-FILE.
       01 PORTFOLIO-RECORD.
           02 PORTFOLIO-STOCK-SYMBOL PIC X(7).
           02 NUMBER-OF-SHARES PIC 9(5).
           02 AVERAGE-COST PIC 9(4)V99.
       FD STOCKS-FILE.
       01 STOCKS-RECORD.
           02 SSYMBOL PIC X(7).
           02 SNAME PIC X(25).
           02 CPRICE PIC 9(4)V99.
       FD REPORT-FILE.
       01 REPORT-LINE PIC X(99).
       WORKING-STORAGE SECTION.
       01 TABLE-INDEX PIC 9(2).
       01 EOF PIC A(1).
       COPY "./STOCKS-TABLE.dat".
      * 01 STOCKS-TABLE OCCURS 20 TIMES.
      *     02 STOCK-SYMBOL PIC X(7).
      *     02 STOCK-NAME PIC X(25).
      *     02 CLOSING-PRICE PIC 9(4)V99.
       01 EQUAL-SIGNS PIC X(98) VALUE ALL '='.
       01 REPORT-HEADER.
           02 STOCK-NAME-HEADER PIC X(10) VALUE 'STOCK NAME'.
           02 FILLER PIC X(17) VALUE SPACES.
           02 SHARES PIC X(7) VALUE '#SHARES'.
           02 FILLER PIC X(3) VALUE SPACES.
           02 UNIT-COST PIC X(9) VALUE 'UNIT COST'.
           02 FILLER PIC X(2) VALUE SPACES.
           02 AT-CLOSING PIC X(10) VALUE 'AT CLOSING'.
           02 FILLER PIC X(4) VALUE SPACES.
           02 COST-BASE PIC X(9) VALUE 'COST BASE'.
           02 FILLER PIC X(2) VALUE SPACES.
           02 MARKET-VALUE-HEADER PIC X(12) VALUE 'MARKET VALUE'.
           02 FILLER PIC X(4) VALUE SPACES.
           02 GAIN-OR-LOSS PIC X(9) VALUE 'GAIN/LOSS'.
       01 READ-COUNTER PIC 9(2).
       01 WRITE-COUNTER PIC 9(2).
       01 FOUND-FLAG PIC A(1).
       01 ADJUSTED-COST-BASE PIC 9(5)V99.
       01 MARKET-VALUE PIC 9(5)V99.
       01 REPORT-RECORD.
           02 REPORT-STOCK-NAME PIC X(25).
           02 FILLER PIC X(3) VALUE SPACES.
           02 REPORT-NUMBER-OF-SHARES PIC ZZ,ZZ9.
           02 FILLER PIC X(3) VALUE SPACES.
           02 REPORT-AVERAGE-COST PIC $$,$$9.99.
           02 FILLER PIC X(3) VALUE SPACES.
           02 REPORT-CLOSING-PRICE PIC $$,$$9.99.
           02 FILLER PIC X(3) VALUE SPACES.
           02 REPORT-ADJUSTED-COST-BASE PIC $$$,$$9.99.
           02 FILLER PIC X(4) VALUE SPACES.
           02 REPORT-MARKET-VALUE PIC $$$,$$9.99.
           02 FILLER PIC X(2) VALUE SPACES.
           02 TOTAL-GAIN-OR-LOSS PIC $$$$,$$9.99-.
       01 REPORT-COUNTERS.
           02 RECORDS-READ PIC X(14) VALUE 'Records read: '.
           02 REPORT-READ-COUNTER PIC Z9.
           02 FILLER PIC X(3) VALUE SPACES.
           02 RECORDS-WRITTEN PIC X(17) VALUE 'Records written: '.
           02 REPORT-WRITE-COUNTER PIC Z9.
       PROCEDURE DIVISION.
       MAIN-PROCEDURE.
           PERFORM INITIALIZATION-RTN.
           PERFORM PROCESS-RTN UNTIL EOF = 'Y'.
           PERFORM CLOSE-RTN.
           STOP RUN.
       INITIALIZATION-RTN.
           PERFORM OPEN-FILES.
           PERFORM LOAD-TABLE VARYING TABLE-INDEX FROM 1 BY 1
               UNTIL EOF = 'Y' OR TABLE-INDEX > 20.
           PERFORM SET-EOF.
           PERFORM WRITE-REPORT-HEADER.
       OPEN-FILES.
           OPEN INPUT PORTFOLIO-FILE STOCKS-FILE.
           OPEN OUTPUT REPORT-FILE.
       LOAD-TABLE.
           PERFORM READ-STOCKS-FILE.
           PERFORM LOAD-DATA.
       READ-STOCKS-FILE.
           READ STOCKS-FILE
               AT END MOVE 'Y' TO EOF
           END-READ.
       LOAD-DATA.
           IF EOF NOT = 'Y'
               MOVE SSYMBOL TO STOCK-SYMBOL(TABLE-INDEX)
               MOVE SNAME TO STOCK-NAME(TABLE-INDEX)
               MOVE CPRICE TO CLOSING-PRICE(TABLE-INDEX)
           END-IF.
       SET-EOF.
           MOVE 'N' TO EOF.
       WRITE-REPORT-HEADER.
           MOVE EQUAL-SIGNS TO REPORT-LINE.
           WRITE REPORT-LINE.
           MOVE REPORT-HEADER TO REPORT-LINE.
           WRITE REPORT-LINE.
           MOVE EQUAL-SIGNS TO REPORT-LINE.
           WRITE REPORT-LINE.
       PROCESS-RTN.
           PERFORM READ-PORTFOLIO-FILE.
           IF EOF NOT = 'Y'
               PERFORM SET-FOUND-FLAG
               PERFORM SEARCH-RTN VARYING TABLE-INDEX FROM 1 BY 1
                   UNTIL FOUND-FLAG = 'Y' OR TABLE-INDEX > 20
           END-IF.
       READ-PORTFOLIO-FILE.
           READ PORTFOLIO-FILE
               AT END MOVE 'Y' TO EOF
               NOT AT END ADD 1 TO READ-COUNTER
           END-READ.
       SET-FOUND-FLAG.
           MOVE 'N' TO FOUND-FLAG.
       SEARCH-RTN.
           IF PORTFOLIO-STOCK-SYMBOL = STOCK-SYMBOL(TABLE-INDEX)
               MOVE 'Y' TO FOUND-FLAG
               MOVE STOCK-NAME(TABLE-INDEX) TO REPORT-STOCK-NAME
               MOVE NUMBER-OF-SHARES TO REPORT-NUMBER-OF-SHARES
               MOVE AVERAGE-COST TO REPORT-AVERAGE-COST
               MOVE CLOSING-PRICE(TABLE-INDEX) TO REPORT-CLOSING-PRICE
               MULTIPLY NUMBER-OF-SHARES BY AVERAGE-COST
                   GIVING ADJUSTED-COST-BASE
               MOVE ADJUSTED-COST-BASE TO REPORT-ADJUSTED-COST-BASE
               MULTIPLY NUMBER-OF-SHARES BY CLOSING-PRICE(TABLE-INDEX)
                   GIVING MARKET-VALUE
               MOVE MARKET-VALUE TO REPORT-MARKET-VALUE
               SUBTRACT ADJUSTED-COST-BASE FROM MARKET-VALUE
                   GIVING TOTAL-GAIN-OR-LOSS
               MOVE REPORT-RECORD TO REPORT-LINE
               WRITE REPORT-LINE
               ADD 1 TO WRITE-COUNTER
           END-IF.
       CLOSE-RTN.
           PERFORM WRITE-REPORT-COUNTERS.
           PERFORM CLOSE-FILES.
       WRITE-REPORT-COUNTERS.
           MOVE EQUAL-SIGNS TO REPORT-LINE.
           WRITE REPORT-LINE.
           MOVE READ-COUNTER TO REPORT-READ-COUNTER.
           MOVE WRITE-COUNTER TO REPORT-WRITE-COUNTER.
           MOVE REPORT-COUNTERS TO REPORT-LINE.
           WRITE REPORT-LINE.
       CLOSE-FILES.
           CLOSE PORTFOLIO-FILE STOCKS-FILE REPORT-FILE.
       END PROGRAM PROJECT-2.
